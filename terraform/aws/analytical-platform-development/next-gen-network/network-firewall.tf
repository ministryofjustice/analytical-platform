resource "aws_networkfirewall_firewall" "main" {
  name                = "${local.application_name}-${local.environment}"
  firewall_policy_arn = aws_networkfirewall_firewall_policy.strict.arn
  vpc_id              = aws_vpc.main.id

  dynamic "subnet_mapping" {
    for_each = {
      "firewall-a" = aws_subnet.main["firewall-a"]
      "firewall-b" = aws_subnet.main["firewall-b"]
      "firewall-c" = aws_subnet.main["firewall-c"]
    }

    content {
      subnet_id = subnet_mapping.value.id
    }
  }
}

resource "aws_networkfirewall_logging_configuration" "cloudwatch" {
  firewall_arn = aws_networkfirewall_firewall.main.arn

  logging_configuration {
    log_destination_config {
      log_destination = {
        logGroup = module.network_firewall_flow_logs_log_group.cloudwatch_log_group_name
      }
      log_destination_type = "CloudWatchLogs"
      log_type             = "FLOW"
    }
    log_destination_config {
      log_destination = {
        logGroup = module.network_firewall_alert_logs_log_group.cloudwatch_log_group_name
      }
      log_destination_type = "CloudWatchLogs"
      log_type             = "ALERT"
    }
  }
}

#### NEW BASED ON (livedatainlinefwpolicy4LU)

resource "aws_networkfirewall_firewall_policy" "stateful" {
  name = "stateful"

  firewall_policy {
    stateless_default_actions          = ["aws:forward_to_sfe"]
    stateless_fragment_default_actions = ["aws:drop"]

    stateful_rule_group_reference {
      resource_arn = aws_networkfirewall_rule_group.stateful.arn
    }

    stateful_rule_group_reference {
      resource_arn = aws_networkfirewall_rule_group.statful_fqdn.arn
    }

    stateful_engine_options {
      rule_order = "DEFAULT_ACTION_ORDER"
    }
  }
}

resource "aws_networkfirewall_rule_group" "stateful" {
  name     = "stateful"
  type     = "STATEFUL"
  capacity = 10000

  rule_group {
    rules_source {
      stateful_rule {
        action = "PASS"
        header {
          destination      = "213.188.195.127/32" # whoami.filippo.io
          destination_port = "22"
          direction        = "ANY"
          protocol         = "TCP"
          source           = "ANY"
          source_port      = "ANY"
        }
        rule_option {
          keyword  = "sid"
          settings = ["1"]
        }
      }
    }

    stateful_rule_options {
      rule_order = "DEFAULT_ACTION_ORDER"
    }
  }
}

resource "aws_networkfirewall_rule_group" "statful_fqdn" {
  name     = "statful-fqdn"
  type     = "STATEFUL"
  capacity = 3000

  rule_group {
    rules_source {
      rules_source_list {
        generated_rules_type = "ALLOWLIST"
        target_types         = ["HTTP_HOST", "TLS_SNI"]
        targets = [
          # Government domains
          ".gov.uk",
          ".justice.gov.uk",

          # Development tools
          ".github.com",
          ".githubusercontent.com",
          ".gitlab.com",

          # Testing connectivity
          ".google.com",

          # Package managers
          ".pypi.org",
          ".pythonhosted.org",
          ".npmjs.org",
          ".npmjs.com",
          ".cran.r-project.org",

          # AWS services
          ".amazonaws.com",
          ".aws.amazon.com",
        ]
      }
    }
  }
}

/*
TODO:
 - Revisit this configuration
   - statful_fqdn works
   - stateful sort of works, it allows SSH to whoami.filippo.io BUT ALSO github.com, which is not intended.
References:
 - https://github.com/ministryofjustice/modernisation-platform/blob/main/terraform/environments/core-network-services/firewall.tf
 - https://github.com/ministryofjustice/modernisation-platform/tree/main/terraform/modules/firewall-policy
 - https://github.com/ministryofjustice/modernisation-platform/tree/main/terraform/environments/core-network-services/firewall-rules
*/

#### NEW GENERATED BY CHATGPT

resource "aws_networkfirewall_firewall_policy" "strict" {
  name = "strict"

  firewall_policy {
    stateless_default_actions          = ["aws:forward_to_sfe"]
    stateless_fragment_default_actions = ["aws:drop"]

    stateful_engine_options {
      rule_order = "STRICT_ORDER"
    }

    # Recommended for STRICT_ORDER so TCP can establish and app-layer rules can match
    stateful_default_actions = [
      "aws:drop_established",
      "aws:alert_established",
    ]

    stateful_rule_group_reference {
      priority     = 1
      resource_arn = "arn:aws:network-firewall:eu-west-2:aws-managed:stateful-rulegroup/AttackInfrastructureStrictOrder"
    }

    stateful_rule_group_reference {
      priority     = 2
      resource_arn = "arn:aws:network-firewall:eu-west-2:aws-managed:stateful-rulegroup/BotNetCommandAndControlDomainsStrictOrder"
    }

    stateful_rule_group_reference {
      priority     = 3
      resource_arn = "arn:aws:network-firewall:eu-west-2:aws-managed:stateful-rulegroup/MalwareDomainsStrictOrder"
    }

    # 1) IP/port allowlist (SSH)
    stateful_rule_group_reference {
      resource_arn = aws_networkfirewall_rule_group.strict.arn
      priority     = 10
    }

    # 2) FQDN allowlist (HTTP host + TLS SNI)
    stateful_rule_group_reference {
      resource_arn = aws_networkfirewall_rule_group.strict_fqdn.arn
      priority     = 20
    }
  }
}

resource "aws_networkfirewall_rule_group" "strict" {
  name     = "strict"
  type     = "STATEFUL"
  capacity = 10000

  rule_group {
    rule_variables {
      ip_sets {
        key = "HOME_NET"
        ip_set {
          definition = [aws_vpc.main.cidr_block]
        }
      }
    }

    rules_source {
      rules_string = <<-EOT
        # Allow SSH only to whoami.filippo.io (213.188.195.127:22)
        pass tcp $HOME_NET any -> 213.188.195.127 22 (msg:"ALLOW SSH to 213.188.195.127:22"; sid:100001; rev:3;)

        # Reject any other outbound SSH (fast fail)
        reject tcp $HOME_NET any -> any 22 (msg:"REJECT outbound SSH not allowlisted"; flow:to_server; sid:100002; rev:3;)
      EOT
    }

    stateful_rule_options {
      rule_order = "STRICT_ORDER"
    }
  }
}

resource "aws_networkfirewall_rule_group" "strict_fqdn" {
  name     = "strict-fqdn"
  type     = "STATEFUL"
  capacity = 3000

  rule_group {
    rule_variables {
      ip_sets {
        key = "HOME_NET"
        ip_set {
          definition = [aws_vpc.main.cidr_block]
        }
      }
    }

    rules_source {
      rules_string = <<-EOT
        # HTTPS allowlist (TCP/443) using SNI
        pass tcp $HOME_NET any -> any 443 (msg:"ALLOW HTTPS SNI .google.com"; flow:to_server,established; tls.sni; content:".google.com"; endswith; sid:210011; rev:2;)
        pass tcp $HOME_NET any -> any 443 (msg:"ALLOW HTTPS SNI google.com";  flow:to_server,established; tls.sni; content:"google.com";  endswith; sid:210012; rev:2;)

        # HTTP allowlist (TCP/80) using Host header
        pass tcp $HOME_NET any -> any 80  (msg:"ALLOW HTTP Host .google.com"; flow:to_server,established; http.host; content:".google.com"; endswith; sid:200011; rev:2;)
        pass tcp $HOME_NET any -> any 80  (msg:"ALLOW HTTP Host google.com";  flow:to_server,established; http.host; content:"google.com";  endswith; sid:200012; rev:2;)
      EOT
    }

    stateful_rule_options {
      rule_order = "STRICT_ORDER"
    }
  }
}
